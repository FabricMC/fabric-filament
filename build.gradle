plugins {
	id 'java-library'
	id 'java-gradle-plugin'
	id 'maven-publish'
	id 'checkstyle'
}

def ENV = System.getenv()

group = 'net.fabricmc'
version = '0.1.0' + (ENV.GITHUB_ACTIONS ? "" : "+local")

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11

	withSourcesJar()
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	options.release = 11
}

repositories {
	mavenCentral()

	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net'
	}
}

dependencies {
	implementation gradleApi()

	implementation "org.ow2.asm:asm:$project.asm_version"
	implementation "org.ow2.asm:asm-tree:$project.asm_version"
	implementation "cuchaz:enigma:$project.enigma_version"
}

gradlePlugin {
	plugins {
		filament {
			id = 'net.fabricmc.filament'
			implementationClass = 'net.fabricmc.filament.FilamentGradlePlugin'
		}
	}
}

checkstyle {
	configFile = file('checkstyle.xml')
	toolVersion = '8.41.1'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_URL) {
			repositories.maven {
				name "fabric"
				url ENV.MAVEN_URL
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}
	}
}
